;All definitions taken from : https://edps.europa.eu/data-protection/data-protection/glossary_en and https://gdpr-info.eu/art-4-gdpr/, unless indicated otherwise


; DATA PROCESSING : 
;"any operation or set of operations which is performed on personal data or on sets of personal data, whether or not by automated means, 
; such as collection, recording, organisation, structuring, storage, adaptation or alteration, retrieval, consultation, use, 
; disclosure by transmission, dissemination or otherwise making available, alignment or combination, restriction, erasure or destruction."

(documentation PersonalDataProcessing EnglishLanguage "any operation or set of operations which is performed on personal data or on sets of personal data, 
whether or not by automated means, such as collection, recording, organisation, structuring, storage, adaptation or alteration, retrieval, 
consultation, use, disclosure by transmission, dissemination or otherwise making available, alignment or combination, restriction, erasure or destruction.")

(termFormat PersonalDataProcessing EnglishLanguage "data processing")

(termFormat PersonalDataProcessing PolishLanguage "przetwarzanie danych")

(subclass PersonalDataProcessing ContentDevelopment)     

(=>
  (instance ?PROC PersonalDataProcessing)
  (exists (?DATA)
    (and
      (result ?PROC ?DATA)
      (instance ?DATA PersonalData))))
    

; Data Collection
        
(documentation DataCollection EnglishLanguage "Data Processing that consists of collecting data")


(termFormat DataCollection EnglishLanguage "data collection")

(termFormat DataCollection PolishLanguage "zbieranie danych")

(subclass DataCollection PersonalDataProcessing)
(subclass DataCollection Getting)     

(=>
  (instance ?DC DataCollection)
  (exists (?DATA)
    (and
      (result ?DC ?DATA)
      (instance ?DATA Collection)
      (memberType ?DATA PersonalData))))
      
      
      
      
      
;Restriction Of Processing

(documentation RestrictionOfProcessing EnglishLanguage "the marking of stored personal data with the aim of limiting their processing in the future")
 
(termFormat RestrictionOfProcessing EnglishLanguage "restriction of processing")

(termFormat RestrictionOfProcessing PolishLanguage "ograniczanie przetwarzania danych")

(subclass RestrictionOfProcessing PersonalDataProcessing)
(subclass RestrictionOfProcessing RegulatoryProcess)

(=>
  (and
    (instance ?RP RestrictionOfProcessing)
    (patient ?RP ?DATA)
    (instance ?PROCESSINGS Collection)
    (memberType ?PROCESSINGS PersonalDataProcessing)
    (memberCount ?PROCESSINGS ?Q1)
    (forall (?PROC)
      (=>
        (member ?PROC ?PROCESSINGS)
        (patient ?PROC ?DATA))))
  (exists (?RESTRICTED ?Q2)
    (and
    (forall (?PROC)
      (=>
        (member ?PROC ?PROCESSINGS)
        (patient ?PROC ?RESTRICTED)))
      (result ?RP ?RESTRICTED)
      (patient ?PROCESSINGS ?RESTRICTED)
      (memberCount ?PROCESSINGS ?Q2)
      (lessThan ?Q1 ?Q2))))
 
      
      
; PROFILING

(documentation Profiling EnglishLanguage "automated processing of personal data consisting of the use of personal data to 
evaluate certain personal aspects relating to a natural person")

(termFormat Profiling EnglishLanguage "profiling")

(termFormat Profiling PolishLanguage "profilowanie")

(subclass Profiling PersonalDataProcessing)


(=>
  (instance ?P Profiling)
  (exists (?PDATA ?PERSON ?ASPECTS ?ASPECT)
    (and
      (result ?P ?PDATA)
      (instance ?PERSON Human)
      (refers ?PDATA ?PERSON)
      (instance ?ASPECTS Collection)
      (represents ?PDATA ?ASPECTS)
      (memberType ?ASPECTS Attribute)
      (forAll
        (=>
          (member ?ASPECT ?ASPECTS)
          (attribute ?PERSON ?ASPECT))))))


; DATA BREACH
 
(documentation PersonalDataBreach EnglishLanguage "a breach of security leading to the accidental or unlawful destruction, 
loss, alteration, unauthorised disclosure of, or access to, personal data transmitted, stored or otherwise processed")

(termFormat PersonalDataBreach EnglishLanguage "personal data breach") 

(termFormat PersonalDataBreach PolishLanguage "wyciek danych osobowych")

(subclass PersonalDataBreach ContentBearingProcess) ;or should it be data processing? 

(=>
  (and
    (instance ?BREACH PersonalDataBreach)
    (patient ?BREACH ?DATA))
  (instance ?DATA PersonalData))
  
  
(=>
  (and
    (instance ?BREACH PersonalDataBreach)
    (patient ?BREACH ?DATA))
   (not (modalAttribute ?BREACH Law)))
   

;pseudonymisation


(documentation Pseudonymisation EnglishLanguage "means the processing of personal data 
in such a manner that the personal data can no longer be attributed to a 
specific data subject without the use of additional information")


(termFormat Pseudonymisation EnglishLanguage "pseudonymisation")

(termFormat Pseudonymisation EnglishLanguage "pseudonimizacja")


(subclass Pseudonymisation PersonalDataProcessing)


(=>
  (and
    (instance ?PSEUDO Pseudonymisation)
    (patient ?PSEUDO ?DATA))
  (instance ?DATA PersonalData))
  
  
(=>
  (and
    (instance ?PSEUDO Pseudonymisation)
    (patient ?PSEUDO ?DATA)
    (refers ?DATA ?SUBJECT)
    (instance ?SUBJECT DataSubject))
  (exists (?NEWDATA)
    (and
      (result ?PSEUDO ?NEWDATA)
      (not
        (identifies ?NEWDATA ?SUBJECT)))))

   
(documentation CrossBorderDataProcessing EnglishLanguage "processing of personal data which takes place in the context of the activities 
of establishments in more than one Member State of a controller or processor in the Union where the controller or processor is established in more than one Member State;
or processing of personal data which takes place in the context of the activities of a single establishment of a controller or processor in the Union but which 
substantially affects or is likely to substantially affect data subjects in more than one Member State.")   
   


(termFormat CrossBorderDataProcessing EnglishLanguage "Cross Border Data Processing")
(termFormat CrossBorderDataProcessing PolishLanguage "przetwarzanie między-państwowe")

(subclass CrossBorderDataProcessing PersonalDataProcessing)
;use involved in event instead
;make a simple base rule
;try to generalize it 
; if CrossBorderDataProcessing and 2 entities are invloved



;say that these countries are different
(=>
  (instance ?CP CrossBorderDataProcessing)
  (exists (?C1 ?C2)
    (and
      (instance ?C1 MemberStateEu)
      (instance ?C2 MemberStateEu)
      (not (equal ?C1 ?C2))
      (involvedInEvent ?CP ?C1))))
      
      

(=>
  (instance ?CP CrossBorderDataProcessing)
  (exists (?COUNTRIES ?CONTROLLER ?CONTROLLERS ?COUNTRY ?N)
    (and
      (or
        (and
          (instance ?CONTROLLERS Collection)
          (memberType ?CONTROLLERS DataProcessor)
          (memberType ?CONTROLLERS DataController)
          (forall (?MEMBER)
            (=>
              (member ?CONTROLLER ?CONTROLLERS)
              (and
                (agent ?CP ?CONTROLLER)
                (member ?COUNTRY ?COUNTRIES)
                (eventLocated ?CP ?COUNTRY)
                (jurisdiction ?CONTROLLER ?COUNTRY)))))
         (and
          (patient ?CP ?COUNTRIES)
          (or
            (instance ?CONTROLLER DataProcessor)
            (instance ?CONTROLLER DataController))))
      (instance ?COUNTRIES Collection)
      (memberType ?COUNTRIES MemberStateEu)
      (memberCount ?COUNTRIES ?N)
      (greaterThan ?N 1))))

 
;PROCESSOR




(documentation DataProcessor EnglishLanguage "A natural or legal person, 
public authority, agency or other body which processes personal data on behalf of the controller.")

(instance DataProcessor SocialRole)

(termFormat DataProcessor EnglishLanguage "data processor")

(termFormat DataProcessor PolishLanguage "procesor danych")


(=>
  (attribute ?DP DataProcessor)
  (exists (?PROC ?CONTROLLER ?CONTROL)
    (and
      (attribute ?CONTROLLER DataController)
      (instance ?CONTROL Managing)
      (agent ?CONTROL ?CONTROLLER)
      (patient ?CONTROL ?PROC)
      (instance ?PROC PersonalDataProcessing)
      (agent ?PROC ?DP))))
      
      
    
; RECORDS 
;DataController hasObligation to maintatin records of PersonalDataProcessing done by DataProcessor


(=>
  (and
    (attribute ?DP DataProcessor)
    (attribute ?DC DataController)
    (instance ?MANAGE Managing)
    (instance ?PROC PersonalDataProcessing)
    (agent ?PROC ?DP)
    (patient ?MANAGE ?DP)
    (agent ?MANAGE ?DC)
    (result ?PROC ?PROCDATA))
  (exists (?RECORD)
    (holdsObligation
      (and
        (instance ?RECORD FactualText)
        (record ?RECORD ?PROCDATA)) ?DC)))

   
;PROCESSOR AGREEMENT

(documentation ProcessorAgreement EnglishLanguage "Transfers of personal data from 
a data controller to a data processor must be secured by a data processor agreement.")

(subclass ProcessorAgreement Contract)

(termFormat ProcessorAgreement EnglishLanguage "processor agreement")

(termFormat ProcessorAgreement PolishLanguage "umowa procesora")

(=>
  (instance ?PA ProcessorAgreement)
  (exists (?PROCESSOR ?CONTROLLER)
    (and
      (attribute ?PROCESSOR DataProcessor)
      (attribute ?CONTROLLER DataController)
      (partyToAgreement ?CONTROLLER ?PA)
      (partyToAgreement ?PROCESSOR ?PA))))
    
;The contract must stipulate that the data processor shall act only on instructions from the data controller. 

(=>
  (and
    (instance ?PA ProcessorAgreement)
    (attribute ?PROCESSOR DataProcessor)
    (attribute ?CONTROLLER DataController)
    (partyToAgreement ?CONTROLLER ?PA)
    (partyToAgreement ?PROCESSOR ?PA))
  (exists (?INSTRUCT ?ACTION)
    (and
      (instance ?INSTRUCT Ordering)
      (result ?INSTRUCT ?ACTION)
      (holdsObligation
        (<=>
          (agent ?ACTION ?PROCESSOR)
          (agent ?INSTRUCT ?CONTROLLER)) ?PROCESSOR))))
        
     
;The data processor must provide sufficient guarantees in respect of the technical security measures and organisational measure governing the processing to be carried out, and must ensure compliance with such measures.



;
;DATA CONTROLLER
;"The data controller is the party that, alone or jointly with others, determines the purposes and means of the processing of personal data."

(documentation DataController EnglishLanguage "The data controller is the party that, 
alone or jointly with others, determines the purposes and means of the processing of personal data.")

(termFormat DataController EnglishLanguage "Data controller")
(termFormat DataController PolishLanguage "kontroler danych")

(instance DataController SocialRole)

(=>
  (and
    (instance ?DC AutonomousAgent)
    (attribute ?DC DataController))
  (exists (?PD ?PROC ?MEANS ?PURPOSE ?DETERMINE)
    (and
      (instance ?PD PersonalData)
      (instance ?PROC PersonalDataProcessing)
      (instance ?DETERMINE Deciding)
      (instance ?MEANS Method)
      (=>
        (and
          (agent ?DETERMINE ?DC)
          (patient ?DETERMINE ?PD))
        (and
          (hasPurpose ?PD ?PURPOSE)
          (attribute ?PROC ?MEANS))))))
            
;DATA MINIMIZATION : means that a data controller should limit the collection of personal information to what is directly relevant and necessary to accomplish a specified purpose  
; In other words, data controllers should collect only the personal data they really need, and should keep it only for as long as they need it
;expressed by the rule below

;definition I am using : "data controllers should only have the data that they need"

;more detailed: if an agent possesses some data, they should only possess it if they have some purpose to possess that data.


(=>
  (and
    (instance ?DC DataController)
    (instance ?DATA PersonalData)
    (possesses ?DATA ?DC))
  (holdsObligation
    (<=>
      (possesses ?DATA ?DC)
      (hasPurposeForAgent ?DATA
        (possesses ?DATA ?DC) ?DC)) ?DC))
        
        
        
(documentation MainEstablishmentOfDataController EnglishLanguage "as regards a controller with establishments in more than one Member State, 
the place of its central administration in the Union, unless the decisions on the purposes and means of the processing of personal data are 
\taken in another establishment of the controller in the Union and the latter establishment has the power to have such decisions implemented, 
in which case the establishment having taken such decisions is to be considered to be the main establishment")


(termFormat MainEstablishmentOfDataController EnglishLanguage "main establishment of a data controller")

(termFormat MainEstablishmentOfDataController EnglishLanguage "główna siedziba kontrolera danych")

(instance MainEstablishmentOfDataController Organization)


(=>
  (instance ?ME MainEstablishmentOfDataController)
  (exists (?GROUP ?A ?M ?ACT)
    (and
      (instance ?GROUP Collection)
      (memberType ?GROUP AutonomousAgent)
      (=>
        (member ?M ?GROUP)
        (attribute ?M DataController))
      (member ?ME ?GROUP)
      (instance ?M Managing)
      (forAll
        (?ACT)
        (=>
          (and
            (agent ?ACT ?M)
            (member ?A ?GROUP))
          (and
            (agent ?M ?ME)
            (patient ?M ?A)))))))


(documentation MainEstablishmentOfDataProcessor EnglishLanguage "as regards a processor with establishments in more than one Member State, 
the place of its central administration in the Union, or, if the processor has no central administration in the Union, 
the establishment of the processor in the Union where the main processing activities in the context of the activities of an 
establishment of the processor take place to the extent that the processor is subject to specific obligations under this Regulation")

(termFormat MainEstablishmentOfDataProcessor EnglishLanguage "main establishment of a data controller")

(termFormat MainEstablishmentOfDataProcessor EnglishLanguage "główna siedziba procesora danych")

(instance MainEstablishmentOfDataProcessor Organization)


(=>
  (instance ?ME MainEstablishmentOfDataController)
  (exists (?GROUP ?A ?M ?ACT)
    (and
      (instance ?GROUP Collection)
      (memberType ?GROUP AutonomousAgent)
      (=>
        (member ?M ?GROUP)
        (attribute ?M DataProcessor))
      (member ?ME ?GROUP)
      (instance ?M Managing)
      (forAll
        (?ACT)
        (=>
          (and
            (agent ?ACT ?M)
            (member ?A ?GROUP))
          (and
            (agent ?M ?ME)
            (patient ?M ?A)))))))
          

;DATA PROTECTION AUTHORUTY (DPA)

(documentation DataProtectionAuthority EnglishLanguage "An independent body that monitors the processing of personal data within its jurisdiction (country, region or international organization);
provides advice to the competent bodies with regard to legislative and administrative measures relating to the processing of personal data;
hears complaints lodged by citizens with regard to the protection of their data protection rights.")

(termFormat DataProtectionAuthority EnglishLanguage "data protection authority")

(termFormat DataProtectionAuthority EnglishLanguage "autorytet ws. ochrony danych")

(subclass DataProtectionAuthority GovernmentOrganization) ;maybe chan

;monitors the processing of personal data within its jurisdiction (country, region or international organization)


;if an entity has a DataProtectionAuthority role and there exists data, that is withing its jursidiction
;then they monitor/supervise that data

;terms: Managing, GeopoliticalArea


(=>
  (and
    (instance ?DPA DataProtectionAuthority)
    (instance ?D PersonalData)
    (instance ?JURISDICTION GeopoliticalArea)
    (jurisdiction ?DPA ?JURISDICTION))
  (exists (?PROC ?MANAGE)
    (and
      (instance ?MANAGE Managing)
      (instance ?PROC PersonalDataProcessing)
      (agent ?MANAGE ?DPA)
      (patient ?MANAGE ?PROC)
      (patient ?PROC ?D))))
      
;provides advice to the competent bodies with regard to legislative and administrative measures relating to the processing of personal data


(=>
  (and
    (instance ?BODY AutonomousAgent)
    (instance ?PD PersonalData)
    (instance ?DPA DataProtectionAuthority)
    (instance ?PROC PersonalDataProcessing)
    (patient ?PROC ?PD)
    (agent ?PROC ?BODY))
  (exists (?ADVISING ?ADVICE)
    (hasPurpose ?DPA
      (and
        (instance ?ADVISING Request)
        (agent ?ADVISING ?DPA)
        (result ?ADVISING ?ADVICE)
        (destination ?ADVICE ?BODY)
        (containsInformation ?ADVICE ?PD)))))

;hears complaints lodged by citizens with regard to the protection of their data protection rights


; the issue I ran into here: How do I define the topic of the complaint being about Data Protection?
; 

(=>
  (and
    (instance ?CITIZEN Human)
    (instance ?JURISDICTION GeopoliticalArea)
    (instance ?DPA DataProtectionAuthority)
    (jurisdiction ?DPA ?JURISDICTION)
    (instance ?Q Questioning)
    (result ?Q ?QUESTION)
    (origin ?Q ?CITIZEN)
    (instance ?PD PersonalData)
    (refers ?PD ?CITIZEN)
    (refers ?QUESTION ?PD))
  (exists (?LISTEN)
    (and
      (instance ?LISTEN Investigating)
      (holdsObligation
        (and
          (agent ?LISTEN ?DPA)
          (patient ?LISTEN ?QUESTION)) ?DPA))))


;instances of DPA in EU:

;FRANCE:

(instance CNIL DataProtectionAuthority)
(jurisdiction CNIL France)


          
                   
;CONFIDENTIALITY
;expressed as a rule on PersonalData
;'share data with a person if and only if they are qualified to receive that information


(=>
  (and
    (instance ?PERSON Human)
    (instance ?DATA PersonalData)
    (possesses ?DATA ?PERSON))
  (exists (?RECEIVER ?GIVE)
    (and
      (instance ?RECEIVER Human)
      (instance ?GIVE Giving)
      (holdsObligation
        (<=>
          (and
            (agent ?GIVE ?PERSON)
            (destination ?GIVE ?RECEIVER)
            (patient ?GIVE ?DATA))
          (modalAttribute
            (destination ?GIVE ?RECEIVER) Permission)) ?PERSON))))
    
; DATA COLLECTION
; TODO: do I need this term? It is not defined in the glossary:https://edps.europa.eu/data-protection/data-protection/glossary_en




;DATA MINING :analysing data from different perspectives and summarising it into useful new information

; my interpretation: If there is some data and it is the patient of Data Mining, then there exists some new information that was extracted from it (the original data is the source of it)
; I define 'new information' as information that is not any other information

(documentation DataMining EnglishLanguage "Analysing data from different perspectives
 and summarising it into useful new information")

(termFormat DataMining EnglishLanguage "Data mining")

(termFormat DataMining EnglishLanguage "wydobywanie danych")


(subclass DataMining PersonalDataProcessing)



(=>
  (and
    (instance ?MINING DataMining)
    (instance ?DATA ContentBearingObject)
    (patient ?MINING ?DATA))
  (exists (?INFO ?OTHERINFO ?OTHEROBJECT)
    (and
      (containsInformation ?DATA ?INFO)
      (result ?MINING ?INFO)
      (containsInformation ?OTHEROBJECT ?OTHERINFO)
      (not
        (equal ?INFO ?OTHERINFO)))))
     

        
        
        
;REPRESENTATIVE





;
; DATA SUBJECT
; The data subject is the person whose personal data are collected, held or processed.

(documentation DataSubject EnglishLanguage "The data subject is the person whose personal data are collected, held or processed.")

(termFormat DataSubject EnglishLanguage "data subject")

(termFormat DataSubject PolishLanguage "podmiot przetwarzania danych")


(subclass DataSubject SocialRole)

(=>
  (and
    (instance ?PERSON Human)
    (instance ?SUBJECT DataSubject)
    (attribute ?PERSON ?SUBJECT))
  (exists (?DATA ?ENTITY ?PROCESSING)
    (and
      (instance ?DATA PersonalData)
      (instance ?ENTITY AutonomousAgent)
      (and
        (instance ?PROCESSING PersonalDataProcessing)
        (agent ?PROCESSING ?ENTITY)
        (patient ?PROCESSING ?DATA)))))

; PERSONAL DATA
;"‘personal data’ means any information relating to an identified or identifiable natural person (‘data subject’); 
; an identifiable natural person is one who can be identified, directly or indirectly, in particular by reference to an identifier such as a name,
; an identification number, location data, an online identifier or to one or more factors specific to the physical, physiological, genetic, mental, economic, cultural or social identity of that natural person;"

(documentation PersonalData EnglishLanguage "‘personal data’ means any information relating to an 
identified or identifiable natural person (‘data subject’); an identifiable natural person is one 
who can be identified, directly or indirectly, in particular by reference to an identifier such as a name, 
an identification number, location data, an online identifier or to one or more factors specific to the physical, 
physiological, genetic, mental, economic, cultural or social identity of that natural person;")


(termFormat DataSubject EnglishLanguage "personal data")

(termFormat DataSubject PolishLangauge "dane osobowe")

(subclass PersonalData ContentBearingObject)

;If data is an instance of Personal Data, then there exists a person that that piece of data refers to and if an agent has access to that data then they can identify the Person from it
(=>
  (instance ?PD PersonalData)
  (exists (?SUBJECT)
    (identifies ?PD ?SUBJECT)))
        
        
(documentation BiometricIdentifying EnglishLanguage "Biometrics or biometric systems are methods for uniquely recognizing humans based upon one or more intrinsic physical or behavioural traits.")
       
(termFormat BiometricIdentifying EnglishLanguage "biometric identifying")

(termFormat BiometricIdentifying PolishLangauge "identyfikacja biometryczna")

(subclass BiometricIdentifying Classifying)

(=>
  (instance ?IDING BiometricIdentifying)
  (exists (?IDENTIFIED ?ID ?NOTIDENTIFIED)
    (and
      (instance ?IDENTIFIED Human)
      (instance ?ID Identifier)
      (result ?IDING ?ID)
      (represents ?ID ?IDENTIFIED)
      (not
        (represents ?ID ?NOTIDENTIFIED)))))
        

(documentation BiometricData EnglishLanguage "Personal data resulting from specific technical processing relating to the physical, 
physiological or behavioural characteristics of a natural person, which allow or confirm the unique identification of that natural person, 
such as facial images or dactyloscopic data")
 
(termFormat BiometricData EnglishLanguage "biometric data")

(termFormat BiometricData PolishLangauge "dane biometryczne")

(subclass BiometricData PersonalData)

(=>
  (instance ?BD BiometricData)
  (exists (?P ?IDING ?ID)
    (and
      (instance ?P Human)
      (instance ?IDING ?BD)
      (result ?IDING ?ID)
      (represents ?ID ?P))))  
      
      
(documentation HealthData EnglishLanguage "personal data related to the physical or mental health of a natural person, 
including the provision of health care services, which reveal information about his or her health status")

(termFormat HealthData EnglishLanguage "health data")

(termFormat HealthData PolishLangauge "dane zdrowotne")


(subclass HealthData PersonalData)

(=>
  (instance ?HD HealthData)
  (exists (?P ?D)
    (and
      (instance ?P Human)
      (instance ?D DiseaseOrSyndrome)
      (part ?D ?P)
      (refers ?HD ?D))))
      
      
(documentation GeneticData EnglishLanguage "means personal data relating to the inherited or acquired genetic characteristics 
of a natural person which give unique information about the physiology or the health of that natural person")

(termFormat GeneticData EnglishLanguage "genetic data")

(termFormat GeneticData PolishLangauge "dane genetyczne")

(subclass GeneticData PersonalData)

(=>
  (instance ?HD GeneticData)
  (exists (?P ?DNA)
    (and
      (instance ?P Human)
      (instance ?DNA DNA)
      (part ?DNA ?P)
      (refers ?HD ?DNA))))
      
      

;CCTV
;It is a television system comprised of a camera or a set of cameras monitoring a specific protected area,
; with additional equipment used for viewing and/or storing the CCTV footage
; this is mapped to SurveillanceSystem in SUMO I believe



;VIDEO SURVEILLANCE

;Question :

(documentation VideoSurveillance EnglishLanguage "Video-surveillance is the monitoring of a specific area, event, activity, 
or person by means of an electronic device or system for visual monitoring. 
Typically, the monitoring is carried out using a SurveillanceSystem.")


(termFormat VideoSurveillance EnglishLanguage "video surveillance")


(subclass VideoSurveillance ContentDevelopment)


;Video surveillance is a Process, that is performed using a camera or a surveillance system and performed on a target 
;Stationary Artifact - use as a word for the target?

(=>
  (instance ?SURVEILLING VideoSurveillance)
  (exists (?DEVICE ?TARGET)
    (and
      (instance ?DEVICE ElectricDevice)
      (instance ?TARGET Physical)
      (instrument ?SURVEILLING ?DEVICE)
      (patient ?SURVEILLING ?TARGET))))
 
; rule saying that Surveiling has the purpose of looking at areas to make sure their safe, modeled as : processes that are patient to surveilling should be less dangerous  

(=>
  (instance ?SURVEILLING VideoSurveillance)
  (exists (?PROCESS)
    (hasPurpose ?SURVEILLING
      (decreasesLikelihood
        (=>
          (and
            (instance ?PROCESS Process)
            (patient ?SURVEILLING ?PROCESS))
          (attribute ?PROCESS Dangerous))))))
         
              

; 24.04-3.05


;DATA PROTECTION


(documentation DataProtection EnglishLanguage "Privacy is the ability of an 
individual to be left alone, out of public view, and in control of 
information about oneself." )  


;Privacy by design


(documentation PassengerNameRecord EnglishLanguage "Information collected by airlines or travel agencies at 
the time a passenger makes a reservation, before travelling. It differs from Advanced Passenger 
Information (API), which is collected later at the time of boarding.." )  

(termFormat PassengerNameRecord EnglishLanguage "Passenger Name Record")


(subclass PassengerNameRecord PersonalData)


(=>
  (instance ?PNR PassengerNameRecord)
  (exists (?TA ?PERSON ?DC)
    (and
      (instance ?PERSON Human)
      (refers ?PNR ?PERSON)
      (instance ?TA TransportationCompany)
      (instance ?DC DataCollection)
      (agent ?DC ?TA)
      (patient ?DC ?PNR))))



;PRIVACY

(documentation RightToPrivacy EnglishLanguage "Privacy is the ability of an 
individual to be left alone, out of public view, and in control of 
information about oneself." )        

(termFormat RightToPrivacy EnglishLanguage "right to privacy")


(subclass RightToPrivacy NormativeAttribute)

(=>
  (and
    (instance ?H Human)
    (attribute ?H RightToPrivacy))
  (hasRight
    (attribute ?H Alone) ?H))
          
          
(=>
  (and
    (instance ?H Human)
    (instance ?INFO PersonalData)
    (refers ?INFO ?H)
    (attrbute ?H RightToPrivacy))
  (exists (?CONTROL)
    (and
      (instance ?CONTROL Guiding)
      (hasRight
        (and
          (agent ?CONTROL ?H)
          (patient ?CONTROL ?INFO)) ?H))))
          
          
(documentation RightToPhysicalPrivacy EnglishLanguage "the ability to prevent 
intrusion in one's physical space")  

(termFormat RightToPhysicalPrivacy EnglishLanguage "right to physical privacy")


(subclass RightToPhysicalPrivacy RightToPrivacy)

(=>
  (and
    (instance ?H Human)
    (attribute ?H RightToPhysicalPrivacy))
  (exists (?SPACE ?INTRUDER)
    (and
      (located ?H ?SPACE)
      (instance ?SPACE PersonalSpace)
      (hasRight
        (not
          (and
            (instance ?INTRUDER Human)
            (located ?INTRUDER ?SPACE))) ?H))))

(documentation RightToInformationalPrivacy EnglishLanguage "the ability to control the collection 
and sharing of information about oneself")

(termFormat RightToInformationalPrivacy EnglishLanguage "right to information privacy")


(subclass RightToInformationalPrivacy RightToPrivacy)


(=>
  (and
    (instance ?H Human)
    (instance ?DP PersonalDataProcessing)
    (patient ?DP ?DATA)
    (refers ?DATA ?H))
  (exists (?MANAGING)
    (and
      (instance ?MANAGING Managing)
      (hasRight
        (and
          (agent ?MANAGING ?H)
          (patient ?MANAGING ?DP)) ?H))))
          
          
          
(=>
  (and
    (instance ?H Human)
    (instance ?DC DataCollection)
    (patient ?DC ?DATA)
    (refers ?DATA ?H))
  (exists (?MANAGING)
    (and
      (instance ?MANAGING Managing)
      (hasRight
        (and
          (agent ?MANAGING ?H)
          (patient ?MANAGING ?DC)) ?H))))


 
          
;FILE SYSTEM


(documentation FilingSystem EnglishLanguage "any structured set of personal data which are accessible according to specific criteria,
 whether centralised, decentralised or dispersed on a functional or geographical basis")
 
(termFormat FilingSystem EnglishLanguage "filing system") 

(termFormat FilingSystem PolishLanguage "system plików") 

(subclass FilingSystem Collection)



(=>
  (instance ?FS FilingSystem)
  (exists (?A ?DP)
    (and
      (memberType ?FS PersonalData)
      (instance ?A AutonomousAgent)
      (instance ?DP PersonalDataProcessing)
      (agent ?DP ?A)
      (patient ?DP ?A))))
 
;RECIPIENT

(documentation RecipientOfPersonalData EnglishLanguage "a natural or legal person, public authority, agency or another body,
 to which the personal data are disclosed, whether a third party or not")

(subclass RecipientOfPersonalData SocialRole)

(termFormat RecipientOfPersonalData EnglishLanguage "recipient") 
(termFormat RecipientOfPersonalData PolishLanguage "odbiorca") 

(=>
  (and
    (instance ?P AutonomousAgent)
    (attribute ?P RecipientOfData))
  (exists (?DATA ?GET)
    (and
      (instance ?DATA PersonalData)
      (instance ?GET Getting)
      (agent ?GET ?P)
      (patient ?GET ?DATA))))
      
; THIRD PARTY 


(documentation ThirdParty EnglishLanguage "means a natural or legal person, public authority, agency or 
body other than the data subject, controller, processor and persons who, under the direct 
authority of the controller or processor, are authorised to process personal data")



(termFormat ThirdParty EnglishLanguage "third party")
(termFormat ThirdParty PolishLanguage "strona trzecia")

(subclass ThirdParty SocialRole)

(=>
  (and
    (instance ?A AutonomousAgent)
    (not (attribute ?A DataController))
    (not (attribute ?A DataSubject))
    (not (attribute ?A DataProcessor))
    (instance ?M Manage)
    (agent ?M ?C)
    (patient ?M ?A)
    (instance ?DP PersonalDataProcessing)
    (agent ?DP ?A)
    (or (attribute ?C DataProcessor) (attribute ?C DataController) ))
  (attribute ?A ThirdParty))

      
; REPRESENTATIVE


(documentation Representative EnglishLanguage "means a natural or legal person established in the Union who, designated by the controller or processor, 
represents the controller or processor with regard to their respective obligations under this Regulation")

(termFormat Representative EnglishLanguage "a representative")
(termFormat Representative PolishLanguage "przedstawiciel")

(subclass Representative SocialRole)

(=>
  (and
    (instance ?A AutonomousAgent)
    (attribute ?A Representative))
  (exists (?DC ?OB)
    (and
      (or
        (attribute ?DC DataController)
        (attribute ?DC DataProcessor))
      (=>
        (holdsObligation ?OB ?DC)
        (holdsObligation ?OB ?A)))))
        
        
; Undertaking

(documentation Undertaking EnglishLanguage "a natural or legal person engaged in an economic activity, irrespective of its legal form")

(termFormat Undertaking EnglishLanguage "an undertaking")

(subclass Undertaking AutonomousAgent)

(=>
  (instance ?U Undertaking)
  (exists (?ACT)
    (and
      (agent ?ACT ?U)
      (instance ?ACT EconomicActivity))))

;Group of Undertakings



(documentation GroupOfUndertakings EnglishLanguage "a controlling undertaking and its controlled undertakings")
(termFormat GroupOfUndertakings EnglishLanguage "a group of undertakings")

(subclass GroupOfUndertakings Collection)(=>
  (instance ?GOU GroupOfUndertakings)
  (exists (?CONTROLLER ?MANAGE ?N)
    (and
      (memberType ?GOU Undertaking)
      (member ?CONTROLLER ?GOU)
      (instance ?MANAGE Managing)
      (agent ?MANAGE ?CONTROLLER)
      (patient ?MANAGE ?GOU)
      (memberCount ?GOU ?N)
      (greaterThan ?N 1))))
      
;Enterprise

(documentation Enterprise EnglishLanguage "a natural or legal person engaged in an economic activity, irrespective of its legal form")

(termFormat Enterprise EnglishLanguage "enterprise")

(subclass Enterprise Undertaking)

(=>
  (instance ?E Enterprise)
  (attribute ?E LegalAgent))

;CONSENT


(documentation ConsentGDPR EnglishLanguage "Consent of the data subject means any freely given, specific, informed and unambiguous indication of 
the data subject’s wishes by which he or she, by a statement or by a clear affirmative action, signifies agreement to the processing of personal 
data relating to him or her")


(termFormat ConsentGDPR EnglishLanguage "consent")
(termFormat ConsentGDPR PolishLanguage "świadoma zgoda")

(subclass ConsentGDPR IntentionalProcess)

(=>
  (and
    (agent ?C ?A)
    (instance ?C ConsentGDPR))
  (exists (?PROC ?DATA ?P)
    (and
      (confersNorm ?A
        (and
          (agent ?PROC ?P)
          (instance ?PROC PersonalDataProcessing)
          (patient ?PROC ?DATA)
          (identifies ?DATA ?A)) Permission)
      (attribute ?A DataSubject)
      (instance ?P AutonomousAgent))))
  
;TERMS RELEVANT TO GDPR, BUT NOT DIRECTLY FROM THE GLOSSARY                       

(documentation EconomicActivity EnglishLanguage "An economic activity is characterised by an input of resources, a production process and an output of products")               
(termFormat EconomicActivity EnglishLanguage "economic activity")


(termFormat EconomicActivity EnglishLanguage "economic activity")
(termFormat EconomicActivity PolishLanguage "aktyność ekonomiczna")

(subclass EconomicActivty IntentionalProcess)

(=>
  (and
    (instance ?ECONACT EconomicActivty)
    (agent ?ECONACT ?AGENT))
  (exists (?PRODUCTION ?PRODUCT ?RESOURCES)
    (and
      (result ?ECONACT ?PRODUCT)
      (agent ?PRODUCTION ?AGENT)
      (instance ?PRODUCTION Creation)
      (instance ?PRODUCT Product))))

;identifies

(documentation identifies EnglishLanguage "(identifies ?OBJ1 ?OBJ2) 
expresses that ?OBJ1 refers to ?OBJ2 and ?OBJ2 is identifiable from ?OBJ1")

(termFormat identifies EnglishLanguage "%1 identifies %2")
(termFormat identifies PolishLanguage "%1 identyfikuje %2")

(subclass identifies refers)

(domain identifies 1 Entity)	
(domain identifies 2 Entity)



(=>
  (identifies ?OBJ1 ?OBJ2)
  (exists (?PERSON)
    (and
      (instance ?PERSON CognitiveAgent)
      (knows ?PERSON
        (refers ?OBJ1 ?OBJ2)))))


;Personal Space


(documentation PersonalSpace EnglishLanguage "the immediate area surrounding a person")

(termFormat PersonalSpace EnglishLanguage "personal space")
(termFormat PersonalSpace PolishLanguage "przestrzeń osobista")

(subclass PersonalSpace Region)


(=>
  (instance ?PS PersonalSpace)
  (exists (?PERSON ?X)
    (and
      (instance ?PERSON Human)
      (located ?PERSON ?PS)
      (=>
        (and
          (instance ?X Object)
          (located ?X ?PS))
        (orientation ?X ?PERSON Near)))))

        



;jurisdiction

;meaning : the right and power to interpret and apply the law


(documentation jurisdiction EnglishLanguage "(@jurisdiction ?AUTONOMOUSAGENT ?AREA) 
expresses that an ?AUTONOMOUSAGENT has power to interpret and apply the law in an ?AREA")


(termFormat jurisdiction EnglishLanguage "%1 has jurisdiction over %2")
(termFormat jurisdiction PolishLanguage "%1 ma jurysdyckcje nas %2")

(domain jurisdiction 1 AutonomousAgent)	
(domain jurisdiction 2 GeopoliticalArea)


(subclass jurisdiction BinaryPredicate)

(=>
  (and
    (instance ?AGENT AutonomousAgent)
    (jurisdiction ?AGENT ?AREA))
  (exists (?LEGALACT ?INTERPRET ?DECIDE)
    (and
      (modalAttribute (agent ?LEGALACT ?AGENT) Law)
      (instance ?INTERPRET Interpreting)
      (instance ?DECIDE LegalDecision)
      (hasRight ?AGENT
        (and
          (agent ?DECIDE ?AGENT)
          (patient ?DECIDE ?LEGALACT)
          (agent ?INTERPRET ?AGENT)
          (patient ?INTERPRET ?LEGALACT))))))

;MemberStateEU

(documentation MemberStateEU EnglishLanguage "Any nation that is a member of the EuropeanUnion")
(termFormat MemberStateEU EnglishLanguage "EU member state")
(termFormat MemberStateEU PolishLanguage "państwo członkowskie UE")


(subclass MemberStateEU Nation)

(=>
  (instance ?STATE MemberStateEU)
  (member ?STATE EuropeanUnion))
  
  
;GDPR subject


;GovernmentRegulatoryBody
;definition taken from https://www.britannica.com/topic/regulatory-agency


;in the US fda, epa
;Government bodies that are charged with advising about the law that is their jurisdiction
;

(documentation GovernmentRegulatoryBody EnglishLanguage "independent governmental body established by 
legislative act in order to set standards in a specific field of activity")

(termFormat GovernmentRegulatoryBody EnglishLanguage "government regulatory body")

(termFormat GovernmentRegulatoryBody PolishLanguage "rządowa organizacja regulacyjna")

(subclass GovernmentRegulatoryBody GovernmentOrganization)

(=>
  (and
    (instance ?DPA GovernmentRegulatoryBody)
    (instance ?JURISDICTION GeopoliticalArea)
    (jurisdiction ?DPA ?JURISDICTION)
    (instance ?LAW RegionalLaw)
    (refers ?LAW ?JURISDICTION)
    (instance ?H AutonomousAgent)
    (located ?H ?JURISDICTION)
    (instance ?Q Questioning)
    (agent ?Q ?H)
    (result ?Q ?QUERY)
    (refers ?QUERY ?LAW)
    (patient ?Q ?DPA))
  (exists (?ANS ?ANSWER)
    (holdsObligation
      (and
        (instance ?ANS Answering)
        (result ?ANS ?ANSWER)
        (agent ?ANS ?DPA)
        (refers ?ANS ?QUERY)) ?DPA)))

